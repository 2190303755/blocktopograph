apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        applicationId "com.protolambda.blocktopograph"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 7
        versionName "1.6"
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def hasKeystore = keystorePropertiesFile.exists()
    def keystoreProperties
    if(hasKeystore) {
        keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }

    signingConfigs {
        if(hasKeystore) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile rootProject.file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }


    productFlavors {
        if(hasKeystore) {
            mainFlavor {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        if(hasKeystore){
            release {
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        opensource {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar', '*.so'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    compile project(':android-leveldb')
    compile project(':tileview')

    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.android.support:design:25.0.1'
    compile 'com.github.bmelnychuk:atv:1.2.8'

    //core is the new recommended alias for analytics
    compile 'com.google.firebase:firebase-core:10.0.1'


}


apply plugin: 'com.google.gms.google-services'
